Smalltalk current createPackage: 'GitHubPage' properties: #{}!
Widget subclass: #GitHubPage
	instanceVariableNames: 'username repositoryDiv'
	category: 'GitHubPage'!

!GitHubPage methodsFor: 'accessing'!

username

	^username
!

username: aString

	username := aString
! !

!GitHubPage methodsFor: 'private'!

sampleRepositoryResponse

'
  {
    "git_url": "git://github.com/dalehenrich/smallsource.git",
    "ssh_url": "git@github.com:dalehenrich/smallsource.git",
    "mirror_url": null,
    "url": "https://api.github.com/repos/dalehenrich/smallsource",
    "has_downloads": true,
    "created_at": "2012-01-30T18:02:07Z",
    "has_issues": false,
    "master_branch": null,
    "description": "Smalltalk source files",
    "forks": 0,
    "svn_url": "https://github.com/dalehenrich/smallsource",
    "html_url": "https://github.com/dalehenrich/smallsource",
    "fork": true,
    "has_wiki": true,
    "private": false,
    "clone_url": "https://github.com/dalehenrich/smallsource.git",
    "homepage": "",
    "size": 128,
    "open_issues": 0,
    "updated_at": "2012-03-01T16:36:38Z",
    "owner": {
      "url": "https://api.github.com/users/dalehenrich",
      "gravatar_id": "5d423de04ebac0b8f9412a9a381c9460",
      "login": "dalehenrich",
      "avatar_url": "https://secure.gravatar.com/avatar/5d423de04ebac0b8f9412a9a381c9460?d=https://a248.e.akamai.net/assets.github.com%2Fimages%2Fgravatars%2Fgravatar-140.png",
      "id": 623951
    },
    "name": "smallsource",
    "watchers": 2,
    "pushed_at": "2012-01-31T23:28:34Z",
    "id": 3307281,
    "language": null
  },
'
! !

!GitHubPage methodsFor: 'rendering'!

renderOn: html

	self renderBodyOn: html.
        html p with: [:h |
                 h
                        button
                                with: 'Refresh repository list';
                                onClick: [  repositoryDiv contents:  [:div | self getRepositories ] ]].
	html strong: 'Repositories'.
     	repositoryDiv := html div.
	self getRepositories
!

renderBodyOn: html

	html p with: [:h |  
		h with: 'Hi there, world. This page is powered by '.
                h 
                        a href: 'http://amber-lang.net/'; 
                        with: 'Amber Smalltalk'.
                h with: '.'].
        html p with: [:h | 
                h with: 'If you''re new to Smalltalk, take an '.
                h
                        a href: 'amber/learn.html'; 
                        with: 'Amber-powered Smaltalk tutorial'.
                h with: ' right in your own browser!!'].
	html p with: [:h |
		h with: 'The list of repositories below is generated by making calls to the'.
		h
			a href: 'http://developer.github.com/';
			with: ' GitHub development API'.
		h with: ' from Amber Smalltalk.' ].
        html p with: [:h |
                 h with: 'If you want to see how this is done: ' .
                 h
                        button
                                with: 'Browse the ', self class name, ' class';
                                onClick: [ Browser openOn: self class ].
    		h with: '.'].
	html p: 'If I were any kind of a web developer (Smalltalk is my game) this page would look a lot prettier.'.
!

renderRepositoryData: repositoryData

	| homepage |
	repositoryDiv contents: [: html |
		html ul: [
			repositoryData do: [:repo | 
				html p: [
					html
                    		    		a href: (repo at: 'html_url'); 
                	        		with: [ html  strong: repo name ].
					(homepage := repo homepage)
						ifNotEmpty: [
							html with: ' ['.
							html
								a href: homepage;
								with: homepage.
							html with: ']'].
					html
						with: ' - ';
						with: repo description]]]]
!

renderRepositoryError: aString
	"append not replace"

	repositoryDiv with: aString
!

getRepositories

	jQuery 
		ajax: 'https://api.github.com/users/', self username, '/repos'
		options: #{
			'type' -> 'GET'.
			'dataType' -> 'jsonp'.
			'success' -> [:repositoryData :status :jqXHR | 
				self renderRepositoryData: repositoryData data ].
     			'error' ->[:jqXHR :status :error | 
				self renderRepositoryError: status printString  ]}.
! !

!GitHubPage class methodsFor: 'instance creation'!

openOn: aString

	(self new)
		username: aString;
          	appendToJQuery: 'body' asJQuery
! !

